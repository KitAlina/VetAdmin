
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Дата = ТекущаяДата();
	СоздатьИзмеренияПланировщика();	 
	
	ОбновитьПериодОтображенияПланировщика();
	ЗаполнитьЖурналЗаписей();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПериодОтображенияПланировщика()
	Начало = НачалоДня(Дата) + 9*3600;
	Конец =  НачалоДня(Дата) + 19*3600;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(Начало, Конец);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЖурналЗаписей()
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Запись.Дата КАК Дата,
		|	Запись.Сотрудник КАК Сотрудник,
		|	Запись.Клиент КАК Клиент,
		|	Запись.Питомец КАК Питомец,
		|	Запись.Услуга КАК Услуга,
		|	Запись.Длительность КАК Длительность,
		|	Запись.Сумма КАК Сумма,
		|	Запись.ДатаОкончания КАК ДатаОкончания,
		|	Запись.Ссылка КАК Ссылка,
		|	Запись.Клиент.НомерТелефона КАК Телефон
		|ИЗ
		|	Документ.Запись КАК Запись
		|ГДЕ
		|	Запись.Проведен = ИСТИНА
		|	И Запись.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗначенияИзмерений = Новый Соответствие;
		ЗначенияИзмерений.Вставить("Услуга", Выборка.Услуга);
		
	                          

		ПредставлениеКлиента = Строка(Выборка.Клиент) + "," + Выборка.Питомец;
	
		ТекстЭлемента = ПредставлениеКлиента + Символы.ПС + Выборка.Телефон;  
		
		
		ЭлементПланировщика = Планировщик.Элементы.Добавить(Выборка.Дата, Выборка.ДатаОкончания);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений);
		ЭлементПланировщика.Значение = Выборка.Ссылка; 
		ЭлементПланировщика.Текст = ТекстЭлемента;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура СоздатьИзмеренияПланировщика()
	Измерение = Планировщик.Измерения.Добавить("Услуга");
	Выборка = Справочники.Услуги.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеИзмерения = Измерение.Элементы.Добавить(Выборка.Ссылка);
		ЗначениеИзмерения.Текст = Выборка.Наименование;  
		ЗначениеИзмерения.ЦветРамки = WebЦвета.БледноЛиловый;
	КонецЦикла;
КонецПроцедуры

  
